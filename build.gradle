plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.diffplug.spotless" version "8.0.0"
}

group = 'com.vn.anhmt'
version = '0.0.1-SNAPSHOT'
description = 'Authentication server, support login by oauth2'

spotless {
    java {
        palantirJavaFormat()
        target 'src/**/*.java'
        targetExclude '**/build/generated/**'
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

compileJava {
    dependsOn(tasks.spotlessApply)
}

tasks.build {
    dependsOn(tasks.check, "test")
}

tasks.check {
    dependsOn(tasks.spotlessCheck)
}

sourceSets {
    generated {
        java {
            srcDirs = ['build/generated-sources/java']
        }
    }

    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }

    integrationTest {
        java {
            srcDirs = ['src/integrationTest/java']
        }
        resources {
            srcDirs = ['src/integrationTest/resources']
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'

    // database
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.apiguardian:apiguardian-api:1.1.2'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.redis.testcontainers:testcontainers-redis:1.6.4'
}

// Task to run only unit tests
tasks.register('unitTest', Test) {
    description = 'Runs unit tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
    testLogging {
        events "failed", "passed", "skipped", "standardError", "standardOut", "started"
    }
}

// Task to run only integration tests
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter unitTest
    testLogging {
        events "failed", "passed", "skipped", "standardError", "standardOut", "started"
    }
}

// Main test task runs both unit and integration tests
tasks.named('test') {
    description = 'Runs all tests (unit + integration).'
    dependsOn unitTest, integrationTest
    // Disable default test execution since we delegate to unitTest and integrationTest
    enabled = false
}